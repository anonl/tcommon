
plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects {
    apply plugin: "maven-publish"
    group = 'nl.weeaboo.common'
    version = '1.2.5'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }    
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "jacoco"

    ext {
        junitVersion = '4.12'
        slf4jVersion = '1.7.21'
        guavaVersion = '19.0' // Guava only used for testing
    }
    
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "com.google.guava:guava-testlib:$guavaVersion"
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }
    
    jacocoTestReport {
	    reports {
	        xml.enabled true
	    }
    }
}

project(':tcommon-core') {
    task aptJar(type: Jar) {
        classifier = 'apt'
        from sourceSets.main.output
        include 'nl/weeaboo/io/CustomSerializable.*'
        include 'nl/weeaboo/io/CustomSerializableAnnotationProcessor.*'
        include 'META-INF/services/javax.annotation.processing.Processor'
    }    
}

project(':tcommon-entity') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-logging') {
    dependencies {
        compile project(':tcommon-core')
        compile "org.slf4j:slf4j-api:$slf4jVersion"
    }
}

publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'tcommon-core'

            def proj = project(":tcommon-core")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        logging(MavenPublication) {
            artifactId = 'tcommon-logging'

            def proj = project(":tcommon-logging")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        entity(MavenPublication) {
            artifactId = 'tcommon-entity'

            def proj = project(":tcommon-entity")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['core', 'entity', 'logging']
    pkg {
        repo = 'tcommon'
        name = 'tcommon'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anonl/tcommon.git'
        version {
            name = project.version
        }
    }
}
