
plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

allprojects {
    apply plugin: 'maven-publish'
    group = 'nl.weeaboo.common'
    version = '2.1.1'

    repositories {
        jcenter()
    }    
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext {
        javaVersion = JavaVersion.VERSION_1_6

        junitVersion = '4.12'
        slf4jVersion = '1.7.21'
        guavaVersion = '19.0' // Guava only used for testing
    }
    
    sourceCompatibility = javaVersion
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "com.google.guava:guava-testlib:$guavaVersion"
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }    
}

project(':tcommon-core') {
}

project(':tcommon-apt6') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-entity') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-logging') {
    dependencies {
        compile project(':tcommon-core')
        compile "org.slf4j:slf4j-api:$slf4jVersion"
    }
}

project(':tcommon-prefsstore') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-test') {
    dependencies {
        compile project(':tcommon-core')
        compile "junit:junit:$junitVersion"
    }
}

publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'tcommon-core'

            def proj = project(":tcommon-core")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        apt6(MavenPublication) {
            artifactId = 'tcommon-apt6'

            def proj = project(":tcommon-apt6")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        logging(MavenPublication) {
            artifactId = 'tcommon-logging'

            def proj = project(":tcommon-logging")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        entity(MavenPublication) {
            artifactId = 'tcommon-entity'

            def proj = project(":tcommon-entity")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        prefsstore(MavenPublication) {
            artifactId = 'tcommon-prefsstore'

            def proj = project(":tcommon-prefsstore")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        test(MavenPublication) {
            artifactId = 'tcommon-test'

            def proj = project(":tcommon-test")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['core', 'apt6', 'entity', 'logging', 'prefsstore', 'test']
    pkg {
        repo = 'tcommon'
        name = 'tcommon'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anonl/tcommon.git'
        version {
            name = project.version
        }
    }
}
