
plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

allprojects {
    apply plugin: 'maven-publish'
    group = 'nl.weeaboo.common'
    version = '2.0.0'

    repositories {
        jcenter()
    }    
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext {
        javaVersion = JavaVersion.VERSION_1_6

        junitVersion = '4.12'
        slf4jVersion = '1.7.21'
        guavaVersion = '19.0' // Guava only used for testing
    }
    
    sourceCompatibility = javaVersion
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "com.google.guava:guava-testlib:$guavaVersion"
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
    
    eclipse.project {
        name = "tcommon-${project.name}"
    }
}

project(':core') {
}

project(':apt6') {
    dependencies {
        compile project(':core')
    }
}

project(':entity') {
    dependencies {
        compile project(':core')
    }
}

project(':logging') {
    dependencies {
        compile project(':core')
        compile "org.slf4j:slf4j-api:$slf4jVersion"
    }
}

project(':prefsstore') {
    dependencies {
        compile project(':core')
    }
}

project(':test') {
    dependencies {
        compile project(':core')
        compile "junit:junit:$junitVersion"
    }
}

publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'tcommon-core'

            def proj = project(":core")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        apt6(MavenPublication) {
            artifactId = 'tcommon-apt6'

            def proj = project(":apt6")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        logging(MavenPublication) {
            artifactId = 'tcommon-logging'

            def proj = project(":logging")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        entity(MavenPublication) {
            artifactId = 'tcommon-entity'

            def proj = project(":entity")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
        prefsstore(MavenPublication) {
            artifactId = 'tcommon-prefsstore'

            def proj = project(":prefsstore")
            from proj.components.java            
            artifact(proj.sourceJar)
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['core', 'apt6', 'entity', 'logging', 'prefsstore', 'test']
    pkg {
        repo = 'tcommon'
        name = 'tcommon'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anonl/tcommon.git'
        version {
            name = project.version
        }
    }
}
